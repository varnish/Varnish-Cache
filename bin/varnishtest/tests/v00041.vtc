varnishtest "Test priv_task"

server s1 {
	rxreq
	txresp
	rxreq
	txresp
} -start

varnish v1 -vcl+backend {
	import ${vmod_debug};
	import ${vmod_std};

	sub vcl_recv {
		set req.http.x0 = debug.test_priv_task(req.url);
	}

	sub vcl_deliver {
		set resp.http.x0 = req.http.x0;
		set resp.http.x1 = debug.test_priv_task("");
	}

	sub vcl_backend_fetch {
		std.log("foo");
		set bereq.http.bx0 = debug.test_priv_task(bereq.url);
		std.log("bar");
	}

	sub vcl_backend_response {
		set beresp.http.bx0 = bereq.http.bx0;
		set beresp.http.bx1 = debug.test_priv_task("");
	}
} -start


client c1 {
	txreq -url /foobar
	rxresp
	expect resp.http.x0 == /foobar
	expect resp.http.x1 == /foobar
	expect resp.http.bx0 == /foobar
	expect resp.http.bx1 == /foobar

	txreq -url /snafu
	rxresp
	expect resp.http.x0 == /snafu
	expect resp.http.x1 == /snafu
	expect resp.http.bx0 == /snafu
	expect resp.http.bx1 == /snafu
} -run
